plugins {
  id "com.github.mxenabled.coppuccino" version "2.0.0"
  id "com.github.mxenabled.vogue" version "1.0.1"
  id "com.github.mxenabled.hush" version "1.0.6"
  // id "com.github.mxenabled.binks" version "0.0.1"
  // id "com.mx.binks" version "0.0.1" // for local testing
  id "groovy"
  id "java"
  id "maven-publish"
  id "java-gradle-plugin"
  id "org.jetbrains.kotlin.jvm" version "1.7.10"
}

group "com.mx.binks"
version "0.0.1"
sourceCompatibility = 1.8

repositories {
  mavenCentral()
  mavenLocal()
  gradlePluginPortal()
  maven { url "https://jitpack.io" }
}

dependencies {
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
  implementation "com.lordcodes.turtle:turtle:0.7.0" // Provides git and commandline interaction API

  constraints {
    implementation ("com.thoughtworks.xstream:xstream:1.4.19") { because "It resolves a bajillion CVEs" }
  }

  // Unit tests
  testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.8.+"
  testImplementation "org.junit.jupiter:junit-jupiter-api:5.8.+"
  testImplementation "org.mockito:mockito-inline:4.+"
  testImplementation "org.spockframework:spock-core:2.2-M1-groovy-3.0"
}

gradlePlugin {
  plugins {
    binksPlugin {
      id = group
      implementationClass = "com.mx.binks.BinksPlugin"
    }
    binksJitPackPlugin {
      id = "com.github.mxenabled.binks"
      implementationClass = "com.mx.binks.BinksPlugin"
    }
  }
}

compileKotlin { kotlinOptions { jvmTarget = "1.8" } }

compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }

coppuccino { kotlin { enabled = true } }

sourceSets {
  test { groovy { srcDirs "src/test/groovy" } }
}

test { useJUnitPlatform() }

wrapper {
  gradleVersion = "7.4.1"
  distributionType = Wrapper.DistributionType.ALL
}
